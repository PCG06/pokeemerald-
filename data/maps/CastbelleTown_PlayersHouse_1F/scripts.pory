const LOCALID_MOM = 1

mapscripts CastbelleTown_PlayersHouse_1F_MapScripts {
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_CASTBELLE_TOWN_STATE, 0: CastbelleTown_PlayerHouse_1F_EventScript_GetPartnerPokemon
        VAR_CASTBELLE_TOWN_STATE, 4: CastbelleTown_PlayerHouse_1F_EventScript_AfterGettingPartnerPokemon
    ]
}

script CastbelleTown_PlayerHouse_1F_EventScript_GetPartnerPokemon {
    lock
    delay(6)
    applymove(LOCALID_MOM, Common_Movement_FaceUp)
    emote(LOCALID_MOM, EMOTE_EXCLAIM)
    applymove(LOCALID_MOM, MomWalkToPlayer)
    msgbox("Mom: {PLAYER}!\n"
            "Did you hear?\p"
            "Prof. Pine is giving out Pokémon to new\n"
            "Trainers!\p"
            "You just got your Trainer Card,\n"
            "why not get yourself a new companion?\p"
            "Prof. Pine would be glad to give you\n"
            "one!\p"
            "Oh! Brian came earlier to meet you.\n"
            "It would be better if you go and meet\l"
            "him before going to Prof. Pine's lab.", MSGBOX_AUTOCLOSE)
    delay(24)
    applymove(LOCALID_MOM, MomWalkToChair)
    applymove(LOCALID_MOM, Common_Movement_FaceLeft)
    setvar(VAR_CASTBELLE_TOWN_STATE, 1)
    setvar(VAR_MAIN_STORY, 2)
    release
    end
}

movement MomWalkToPlayer {
    walk_up
    walk_right * 3
    walk_up
}

movement MomWalkToChair {
    walk_down
    walk_left * 3
    walk_down
}

script CastbelleTown_PlayerHouse_1F_EventScript_AfterGettingPartnerPokemon {
    lock
    turnobject(LOCALID_MOM, DIR_SOUTH)
    emote(LOCALID_MOM, EMOTE_EXCLAIM)
    applymove(LOCALID_MOM, MomWalkToPlayer_2)
    msgbox("Mom: Hello, honey!\p"
            "Oh, you got your first Pokémon,\n"
            "already?\p"
            "That's great!\n"
            "I am overjoyed for you!\l"
            "I hope you two bond well!\p"
            "Now that you're going on your own\n"
            "adventure, you might need these!", MSGBOX_AUTOCLOSE)
    giveitem(ITEM_REPEL, 5)
    giveitem(ITEM_TM_RETURN)
    msgbox("Mom: These items will help you out in\n"
            "your adventure!\p"
            "Please, take care, honey!\n"
            "If you're ever tired, you can always\l"
            "come home and take rest!\p"
	        "… … … … … … … …\n"
	        "… … … … … … … …", MSGBOX_AUTOCLOSE)
    applymove(LOCALID_MOM, MomWalkToChair_2)
    delay(24)
    setvar(VAR_CASTBELLE_TOWN_STATE, 5)
    end
}

movement MomWalkToPlayer_2 {
    walk_right * 3
    walk_down
}

movement MomWalkToChair_2 {
    walk_left * 3
    walk_up
    face_left
}

script CastbelleTown_PlayersHouse_1F_EventScript_Mom {
    lock
    faceplayer
    if (var(VAR_CASTBELLE_TOWN_STATE) >= 1 && var(VAR_CASTBELLE_TOWN_STATE) <= 3) {
        checkplayergender
        if (var(VAR_RESULT) == MALE) {
            msgbox("Mom: sniff…\p"
                    "My little boy going on his own\n"
                    "adventure.\p"
                    "I know you were waiting your whole life\n"
                    "for this moment.\p"
                    "Now go get your partner!", MSGBOX_AUTOCLOSE)
        }
        else {
            msgbox("Mom: sniff…\p"
                    "My little girl going on her own\n"
                    "adventure.\p"
                    "I know you were waiting your whole life\n"
                    "for this moment.\p"
                    "Now go get your partner!", MSGBOX_AUTOCLOSE)
        }
        faceoriginaldir
    }
    else {
        msgbox("Mom: Hello, honey!\p"
                "Feeling tired? Take some rest.", MSGBOX_AUTOCLOSE)
        call(Common_EventScript_OutOfCenterPartyHeal)
        msgbox("Mom: There!\n"
                "Feeling all refreshed, {PLAYER}?", MSGBOX_AUTOCLOSE)
    }
    release
    end
}
