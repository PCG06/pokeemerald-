const LOCALID_MOM = 1

mapscripts CastbelleTown_PlayersHouse_1F_MapScripts {
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_CASTBELLE_TOWN_STATE, 0: CastbelleTown_PlayerHouse_1F_EventScript_GetPartnerPokemon
        VAR_CASTBELLE_TOWN_STATE, 4: CastbelleTown_PlayerHouse_1F_EventScript_AfterGettingPartnerPokemon
    ]
}

script CastbelleTown_PlayerHouse_1F_EventScript_GetPartnerPokemon {
    lock
    delay(6)
    applymove(LOCALID_MOM, Common_Movement_FaceUp)
    emote(LOCALID_MOM, EMOTE_EXCLAIM)
    applymove(LOCALID_MOM, moves(walk_up
                            walk_right * 3
                            walk_up))
    speakername("Mom")
    msgbox("{PLAYER}!\n"
            "Did you hear?\p"
            "Prof. Pine is giving Pokémon to new\n"
            "Trainers!\p"
            "You just got your Trainer Card,\n"
            "why not get yourself a new companion?\p"
            "Prof. Pine would be happy to give you\n"
            "one!\p"
            "Oh, and Brian stopped by earlier.\n"
            "It might be a good idea to see him first\l"
            "before heading to Prof. Pine's lab.", MSGBOX_AUTOCLOSE)
    delay(24)
    applymove(LOCALID_MOM, moves(walk_down
                            walk_left * 3
                            walk_down))
    applymove(LOCALID_MOM, Common_Movement_FaceLeft)
    setvar(VAR_CASTBELLE_TOWN_STATE, 1)
    setvar(VAR_MAIN_STORY, 2)
    release
    end
}

script CastbelleTown_PlayerHouse_1F_EventScript_AfterGettingPartnerPokemon {
    lock
    turnobject(LOCALID_MOM, DIR_SOUTH)
    emote(LOCALID_MOM, EMOTE_EXCLAIM)
    applymove(LOCALID_MOM, moves(walk_right * 3
                                walk_down))
    speakername("Mom")
    msgbox("Hello, honey!\p"
            "Oh, you got your first Pokémon,\n"
            "already?\p"
            "That's great!\n"
            "I am overjoyed for you!\l"
            "I hope you two bond well!\p"
            "Now that you're going on your own\n"
            "adventure, you might need these!", MSGBOX_AUTOCLOSE)
    giveitem(ITEM_REPEL, 5)
    giveitem(ITEM_TM_RETURN)
    setflag(FLAG_SYS_RECEIVED_TM_MOVES)
    setflag(FLAG_SYS_RECEIVED_TUTOR_MOVES)
    speakername("Mom")
    msgbox("These items will help you out in please\n"
            "adventure!\p"
            "Please, take care, honey!\n"
            "If you're ever tired, you can always\l"
            "come home and take rest!\p"
	        "… … … … … … … …\n"
	        "… … … … … … … …", MSGBOX_AUTOCLOSE)
    applymove(LOCALID_MOM, moves(walk_left * 3
                                walk_up
                                face_left))
    delay(24)
    setvar(VAR_CASTBELLE_TOWN_STATE, 5)
    end
}

script CastbelleTown_PlayersHouse_1F_EventScript_Mom {
    lock
    faceplayer
    if (var(VAR_CASTBELLE_TOWN_STATE) >= 1 && var(VAR_CASTBELLE_TOWN_STATE) <= 3) {
        checkplayergender
        speakername("Mom")
        if (var(VAR_RESULT) == MALE) {
            msgbox("{FONT_SMALL}sniff…{FONT_NORMAL}\p"
                    "My little boy going on his own\n"
                    "adventure.\p"
                    "I know you were waiting your whole life\n"
                    "for this moment.\p"
                    "Before you head to Professor Pine's lab,\n"
                    "why don't you go meet Brian?\p"
                    "He's been waiting to catch up with you.\p"
                    "Now go on, {PLAYER}!", MSGBOX_AUTOCLOSE)

        }
        else {
            msgbox("{FONT_SMALL}sniff…{FONT_NORMAL}\p"
                    "My little girl going on her own\n"
                    "adventure.\p"
                    "I know you were waiting your whole life\n"
                    "for this moment.\p"
                    "Before you head to Professor Pine's lab,\n"
                    "why don't you go meet Brian?\p"
                    "He's been waiting to catch up with you.\p"
                    "Now go on, {PLAYER}!", MSGBOX_AUTOCLOSE)

        }
        faceoriginaldir
    }
    else {
        speakername("Mom")
        msgbox("Hello, honey!\p"
                "Feeling tired? Take some rest.", MSGBOX_AUTOCLOSE)
        call(Common_EventScript_OutOfCenterPartyHeal)
        speakername("Mom")
        msgbox("There!\n"
                "Feeling all refreshed, {PLAYER}?", MSGBOX_AUTOCLOSE)
    }
    release
    end
}
