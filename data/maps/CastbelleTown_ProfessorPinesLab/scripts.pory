const LOCALID_PROF_PINE = 1
const GRASS_BALL = 2
const FIRE_BALL = 3
const WATER_BALL = 4
const LOCALID_BRIAN = 6

mapscripts CastbelleTown_ProfessorPinesLab_MapScripts {}

script CastbelleTown_ProfessorPinesLab_EventScript_ProfessorPine {
    speakername("Prof. Pine")
    if (var(VAR_CASTBELLE_TOWN_STATE) == 3) {
        msgbox("Go ahead, {PLAYER}!\p"
        "Choose your first Pokémon.", MSGBOX_NPC)
    }
    elif (var(VAR_CASTBELLE_TOWN_STATE) == 4) {
        msgbox("Safe travels, {PLAYER}.\n"
                "Don't forget to meet your mother\l"
                "before you leave.\p"
                "It's rude to leave without goodbyes.", MSGBOX_NPC)
    }
    elif (var(VAR_CASTBELLE_TOWN_STATE) == 5) {
        msgbox("Please when you meet Brian,\n"
                "give him his Pokédex, {PLAYER}.\p"
                "He must be wandering around Castbelle\n"
                "Path!", MSGBOX_NPC)
    }
    else {
        msgbox("Hey, {PLAYER}!\p"
                "I hope your adventure goes smoothly!", MSGBOX_NPC)
    }
    end
}

script CastbelleTown_ProfessorPinesLab_EventScript_ProfessorPineGivesPokemon {
    lock
    applymoveplayer(Common_Movement_WalkUp)
    delay(24)
    speakername("Prof. Pine")
    msgbox("Hello, {PLAYER}!\p"
            "You're here to get your first Pokémon,\n"
            "huh?\p"
            "Where's Brian?\n"
            "I thought he was along with you?", MSGBOX_AUTOCLOSE)
    msgbox("… … … … … … …", MSGBOX_AUTOCLOSE)
    speakername("Prof. Pine")
    msgbox("Oh I see. Anyway, since you're here,\n"
            "you'd want to meet your first Pokémon\l"
            "now, right?", MSGBOX_AUTOCLOSE)
    applymove(LOCALID_PROF_PINE, Common_Movement_WalkDown)
    setobjectxyperm(LOCALID_PROF_PINE, 6, 4)
    speakername("Prof. Pine")
    msgbox("In these three Poké Balls are three\n"
            "Pokémon I was gifted by an older\l"
            "student of mine.\p"
            "You can now go ahead and\n"
            "choose one, {PLAYER}!", MSGBOX_AUTOCLOSE)
    setvar(VAR_CASTBELLE_TOWN_STATE, 3)
    release
    end
}

script CastbelleTown_ProfessorPinesLab_EventScript_GrassStarter {
    lock
    if (var(VAR_CASTBELLE_TOWN_STATE) <= 2) {
        msgbox("It's Prof. Pine's starter Pokémon!", MSGBOX_AUTOCLOSE)
    }
    elif (var(VAR_CASTBELLE_TOWN_STATE) == 3) {
        playmoncry(SPECIES_CHESPIN, CRY_MODE_NORMAL)
        showmonpic(SPECIES_CHESPIN, 10, 3)
        speakername("Prof. Pine")
        msgbox("Ah, the Grass-type\n"
                "Chespin, I see!\p"
                "Chespin is quite easy to raise!\p"
                "It's the perfect Pokémon for beginners.\p"
                "So, do you choose the\n"
                "Grass-type Chespin as your first\l"
                "Pokémon?", MSGBOX_YESNO)
        if (var(VAR_RESULT) == YES) {
	        bufferspeciesname(STR_VAR_1, SPECIES_CHESPIN)
            givemon(SPECIES_CHESPIN, 5, ITEM_ORAN_BERRY)
            setvar(VAR_STARTER_MON, STARTER_GRASS)
            setvar(VAR_TEMP_0, SPECIES_CHESPIN)
            goto(CastbelleTown_ProfessorPinesLab_EventScript_ReceivedMon)
        }
        else {
            hidemonpic
            speakername("Prof. Pine")
            msgbox("Okay, take your time.\p"
                    "This is an important decision.", MSGBOX_AUTOCLOSE)
        }
    }
    else {
        msgbox("It's Prof. Pine's other starter Pokémon.", MSGBOX_AUTOCLOSE)
    }
    release
    end
}

script CastbelleTown_ProfessorPinesLab_EventScript_FireStarter {
    lock
    if (var(VAR_CASTBELLE_TOWN_STATE) <= 2) {
        msgbox("It's Prof. Pine's starter Pokémon!", MSGBOX_AUTOCLOSE)
    }
    elif (var(VAR_CASTBELLE_TOWN_STATE) == 3) {
        playmoncry(SPECIES_LITTEN, CRY_MODE_NORMAL)
        showmonpic(SPECIES_LITTEN, 10, 3)
        speakername("Prof. Pine")
        msgbox("Ah, the Fire-type\n"
                "Litten, I see!\p"
                "Litten can be a challenge to raise!\p"
                "Bonding with your Litten would be\n"
                "your ultimate test as a Trainer!\p"
                "So, do you choose the\n"
                "Fire-type Litten as your first\l"
                "Pokémon?", MSGBOX_YESNO)
        if (var(VAR_RESULT) == YES) {
	        bufferspeciesname(STR_VAR_1, SPECIES_LITTEN)
            givemon(SPECIES_LITTEN, 5, ITEM_ORAN_BERRY)
            setvar(VAR_STARTER_MON, STARTER_FIRE)
            setvar(VAR_TEMP_0, SPECIES_LITTEN)
            goto(CastbelleTown_ProfessorPinesLab_EventScript_ReceivedMon)
        }
        else {
            hidemonpic
            speakername("Prof. Pine")
            msgbox("Okay, take your time.\p"
                "This is an important decision.", MSGBOX_AUTOCLOSE)
        }
    }
    else {
        msgbox("It's Prof. Pine's other starter Pokémon.", MSGBOX_AUTOCLOSE)
    }
    release
    end
}

script CastbelleTown_ProfessorPinesLab_EventScript_WaterStarter {
    lock
    if (var(VAR_CASTBELLE_TOWN_STATE) <= 2) {
        msgbox("It's Prof. Pine's starter Pokémon!", MSGBOX_AUTOCLOSE)
    }
    elif (var(VAR_CASTBELLE_TOWN_STATE) == 3) {
        playmoncry(SPECIES_PIPLUP, CRY_MODE_NORMAL)
        showmonpic(SPECIES_PIPLUP, 10, 3)
        speakername("Prof. Pine")
        msgbox("Ah, the Water-type\n"
                "Piplup, I see!\p"
                "Piplup is the most balanced of the\n"
                "three!\p"
                "You need to take care of this one,\n"
                "though! They are clumsy.\p"
                "So, do you choose the\n"
                "Water-type Piplup as your first\l"
                "Pokémon?", MSGBOX_YESNO)
        if (var(VAR_RESULT) == YES) {
	        bufferspeciesname(STR_VAR_1, SPECIES_PIPLUP)
            givemon(SPECIES_PIPLUP, 5, ITEM_ORAN_BERRY)
            setvar(VAR_STARTER_MON, STARTER_WATER)
            setvar(VAR_TEMP_0, SPECIES_PIPLUP)
            goto(CastbelleTown_ProfessorPinesLab_EventScript_ReceivedMon)
        }
        else {
            hidemonpic
            speakername("Prof. Pine")
            msgbox("Okay, take your time.\p"
                    "This is an important decision.", MSGBOX_AUTOCLOSE)
        }
    }
    else {
        msgbox("It's Prof. Pine's other starter Pokémon.", MSGBOX_AUTOCLOSE)
    }
    release
    end
}

script CastbelleTown_ProfessorPinesLab_EventScript_ReceivedMon {
    hidemonpic
    playmoncry(VAR_TEMP_0, CRY_MODE_NORMAL)
	delay(40)
	waitmoncry
    removeobject(VAR_LAST_TALKED)
    playfanfare(MUS_DP_OBTAIN_ITEM)
    speakername(NULL)
	message("{PLAYER} received the {STR_VAR_1}\n"
            "from Prof. Pine!")
	waitfanfare
    closemessage
    call(Common_EventScript_ShowPokemonNature)
    speakername("Prof. Pine")
	msgbox("Would you like to nickname your\n"
            "{STR_VAR_1}?", MSGBOX_YESNO)
    if (var(VAR_RESULT) == YES) {
	    call(Common_EventScript_GetGiftMonPartySlot)
	    call(Common_EventScript_NameReceivedPartyMon)
    }
    closemessage
    setflag(FLAG_SYS_POKEMON_GET)
    setvar(VAR_CASTBELLE_TOWN_STATE, 4)
    goto(CastbelleTown_ProfessorPinesLab_EventScript_EnterBrian)
    end
}

script CastbelleTown_ProfessorPinesLab_EventScript_EnterBrian {
    lock
    playse(SE_EXIT)
    waitse
    speakername("???")
    msgbox("I'M HERE!", MSGBOX_AUTOCLOSE)
    setflag(FLAG_HIDE_CASTBELLE_TOWN_BERRY_SHOP_BRIAN)
    emotenowait(LOCALID_PROF_PINE, EMOTE_QUESTION)
    emotenowait(OBJ_EVENT_ID_PLAYER, EMOTE_QUESTION)
    playbgm(MUS_DP_RIVAL, TRUE) // So that the music plays even after battle ends
    applymoveplayer(Common_Movement_FaceDown)
    addobject(LOCALID_BRIAN)
    setobjectxy(LOCALID_BRIAN, 6, 11)
    applymove(LOCALID_BRIAN, BrianRushToProf)
    applymoveplayer(Common_Movement_FaceLeft)
    speakername("Brian")
    msgbox("Sorry, I'm late, professor!\p"
            "I was buying some berries and may have\n"
            "forgotten about this!\p"
            "That was a lie!\n"
            "I would never forget something as\l"
            "important as this!\p"
            "I'm really sorry, Prof. Pine!", MSGBOX_AUTOCLOSE)
    speakername("Prof. Pine")
    msgbox("It's okay, Brian!\n"
            "We all get caught up in our hobbies.\p"
            "Now wouldn't you want to choose your\n"
            "partner Pokémon?", MSGBOX_AUTOCLOSE)    
    applymove(LOCALID_BRIAN, Common_Movement_WalkInPlaceFasterUp)
    speakername("Brian")
    msgbox("YES!\p"
            "I choose you, {STR_VAR_1}!", MSGBOX_AUTOCLOSE)
    emotenowait(LOCALID_PROF_PINE, EMOTE_EXCLAIM)
    emote(OBJ_EVENT_ID_PLAYER, EMOTE_EXCLAIM)
    speakername("Prof. Pine")
    msgbox("Uhh, sorry to be the bearer of bad news,\n"
            "but {PLAYER} has already chosen\l"
            "{STR_VAR_1}…", MSGBOX_AUTOCLOSE)
    emote(LOCALID_BRIAN, EMOTE_EXCLAIM)
    speakername("Brian")
    msgbox("…\n"
            "…\l"
            "Well, the Early Bird gets the Wurmple…\p"
            "Then in that case…", MSGBOX_AUTOCLOSE)
    if (flag(FLAG_HIDE_CASTBELLE_TOWN_PROF_PINES_LAB_GRASS_STARTER) == true) {
        applymove(LOCALID_BRIAN, BrianWalkToFireStarter)
        applymoveplayer(Common_Movement_FaceRight)
        speakername("Brian")
        msgbox("I choose you, Litten!", MSGBOX_AUTOCLOSE)
        playmoncry(SPECIES_LITTEN, CRY_MODE_NORMAL)
	    delay(40)
	    waitmoncry
        removeobject(FIRE_BALL)
        applymove(LOCALID_BRIAN, Common_Movement_FaceLeft)
    }
    elif (flag(FLAG_HIDE_CASTBELLE_TOWN_PROF_PINES_LAB_FIRE_STARTER) == true) {
        applymove(LOCALID_BRIAN, BrianWalkToWaterStarter)
        applymoveplayer(Common_Movement_FaceRight)
        speakername("Brian")
        msgbox("I choose you, Piplup!", MSGBOX_AUTOCLOSE)
        playmoncry(SPECIES_PIPLUP, CRY_MODE_NORMAL)
	    delay(40)
	    waitmoncry
        removeobject(WATER_BALL)
        applymove(LOCALID_BRIAN, Common_Movement_FaceLeft)
    }
    else {
        applymove(LOCALID_BRIAN, BrianWalkToGrassStarter)
        speakername("Brian")
        msgbox("I choose you, Chespin!", MSGBOX_AUTOCLOSE)
        playmoncry(SPECIES_CHESPIN, CRY_MODE_NORMAL)
	    delay(40)
	    waitmoncry
        removeobject(GRASS_BALL)
        applymove(LOCALID_BRIAN, Common_Movement_FaceRight)
    }
    speakername("Brian")
    msgbox("Okay, {PLAYER}!\n"
            "Now that we have our own Pokémon,\l"
            "let's battle em!", MSGBOX_DEFAULT)
    switch (var(VAR_STARTER_MON))
    {
        default:
        case STARTER_GRASS:
            trainerbattle_no_intro_no_whiteout(TRAINER_BRIAN_1_GRASS, "Ow man!\pYou got lucky!")

        case STARTER_FIRE:
            trainerbattle_no_intro_no_whiteout(TRAINER_BRIAN_1_FIRE, "Ow man!\pYou got lucky!")

        case STARTER_WATER:
            trainerbattle_no_intro_no_whiteout(TRAINER_BRIAN_1_WATER, "Ow man!\pYou got lucky!")
    }
    special(HealPlayerParty)
    speakername("Brian")
    msgbox("That was fun!", MSGBOX_AUTOCLOSE)
    applymove(LOCALID_BRIAN, Common_Movement_FaceUp)
    speakername("Brian")
    msgbox("Thank you, Prof. Pine for my new best\n"
            "friend!")
    emote(OBJ_EVENT_ID_PLAYER, EMOTE_EXCLAIM)
    applymove(LOCALID_BRIAN, Common_Movement_FacePlayer)
    speakername("Brian")
    msgbox("Hehe! I'm only joking, {PLAYER}!\p"
            "Anyway, I'm off to Sola City!\n"
            "I wanna catch the ferry to the\l"
            "mainland and challenge the Gym Leader!", MSGBOX_AUTOCLOSE)
    applymove(LOCALID_BRIAN, BrianExitLab)
    removeobject(LOCALID_BRIAN)
    emote(LOCALID_PROF_PINE, EMOTE_EXCLAIM)
    speakername("Prof. Pine")
    msgbox("Oh! Brian! Wait!", MSGBOX_DEFAULT)
    delay(6)
    playse(SE_EXIT)
    delay(24)
    speakername("Prof. Pine")
    msgbox("sigh…\p"
            "Kids these days…", MSGBOX_AUTOCLOSE)
    playbgm(MUS_HG_ELM_LAB, TRUE) // Back to original music
    delay(24)
    goto(CastbelleTown_ProfessorPinesLab_EventScript_ProfPineGivesPokedex)
    end
}

movement BrianRushToProf {
    walk_up * 6
}

movement BrianExitLab {
    walk_down * 6
}

movement BrianWalkToFireStarter {
    walk_down
    walk_right * 2
    walk_up
}

movement BrianWalkToWaterStarter {
    walk_down
    walk_right * 3
    walk_up
}

movement BrianWalkToGrassStarter {
    walk_right
    face_up
}

script CastbelleTown_ProfessorPinesLab_EventScript_ProfPineGivesPokedex {
    getplayerxy(VAR_0x8000, VAR_0x8001)
    switch (var(VAR_0x8000)) {
        case 7:
            applymove(LOCALID_PROF_PINE, ProfPineWalkToPlayer_1)
        case 8:
            applymove(LOCALID_PROF_PINE, ProfPineWalkToPlayer_2)
        case 9:
            applymove(LOCALID_PROF_PINE, ProfPineWalkToPlayer_3)
    }
    turnplayer(DIR_WEST)
    speakername("Prof. Pine")
    msgbox("{PLAYER}!\n"
            "Now, for the main reason why I need\l"
            "your help!\p"
            "This is the Pokédex!", MSGBOX_AUTOCLOSE)
	playfanfare(MUS_OBTAIN_ITEM)
    speakername(NULL)
    message("{PLAYER} received the Pokédex!")
	waitfanfare
    closemessage
	setflag(FLAG_SYS_POKEDEX_GET)
	special(SetUnlockedPokedexFlags)
	setflag(FLAG_RECEIVED_POKEDEX_FROM_PROF_PINE)
    speakername("Prof. Pine")
    msgbox("The Pokédex is a high-tech tool that\n"
            "records all Pokémon you see or capture.\p"
            "Whenever you catch a Pokémon,\n"
            "the Pokédex records its data and it\l"
            "gets sent to me!\p"
            "I use that data for my research.\n"
            "It helps me to know what moves a\l"
            "Pokémon can learn, what their traits\l"
            "are, where they can be commonly found,\l"
            "and their sizes and weights!\p"
            "For your fieldwork, all you need to do\n"
            "is go out there and catch any Pokémon\l"
            "you see.\p"
            "Of course, to catch Pokémon, you will\n"
            "need Poké Balls!", MSGBOX_AUTOCLOSE)
    giveitem(ITEM_POKE_BALL, 10)
    speakername("Prof. Pine")
    msgbox("The Poké Ball is a capsule that catches\n"
            "a Pokémon.\p"
            "The Poké Ball becomes the home of the\n"
            "Pokémon.\p"
            "But it won't always work!\n"
            "You'd need to weaken the wild Pokémon\l"
            "first, then throw a Poké Ball.\p"
            "Also, note that you cannot catch other\n"
            "Trainer's Pokémon!\p"
            "Due to recent events, I feel like I'd\n"
            "need to announce it now.\p"
            "Also, Brian was supposed to get his\n"
            "Pokédex right now, too.\p"
            "But, he ran off in a hurry.\n"
            "So, I need you to find him and give\l"
            "him his Pokédex.\p"
            "He must be wandering around Castbelle\n"
            "Path right now.\p"
            "Anyway, {PLAYER}!\p"
            "I wish you all the best in your Pokémon\n"
            "adventure!\p"
            "Safe travels, {PLAYER}.\n"
            "Don't forget to meet your mother\l"
            "before you leave.\p"
            "It's rude to leave without goodbyes.", MSGBOX_NPC)
    setvar(VAR_MAIN_STORY, 4)
    release
    end
}

movement ProfPineWalkToPlayer_1 {
    walk_down
    face_right
}

movement ProfPineWalkToPlayer_2 {
    walk_down
    walk_right
}

movement ProfPineWalkToPlayer_3  {
    walk_down
    walk_right * 2
}

script CastbelleTown_ProfessorPinesLab_EventScript_ProfessorPineGivesPokemon_Trigger1 {
    applymoveplayer(PlayerToCenterx2_Left)
    goto(CastbelleTown_ProfessorPinesLab_EventScript_ProfessorPineGivesPokemon)
    end
}

movement PlayerToCenterx2_Left {
    walk_right * 2
}

script CastbelleTown_ProfessorPinesLab_EventScript_ProfessorPineGivesPokemon_Trigger2 {
    applymoveplayer(PlayerToCenterx1_Left)
    goto(CastbelleTown_ProfessorPinesLab_EventScript_ProfessorPineGivesPokemon)
    end
}

movement PlayerToCenterx1_Left {
    walk_right
}

script CastbelleTown_ProfessorPinesLab_EventScript_ProfessorPineGivesPokemon_Trigger4 {
    applymoveplayer(PlayerToCenterx1_Right)
    goto(CastbelleTown_ProfessorPinesLab_EventScript_ProfessorPineGivesPokemon)
    end
}

movement PlayerToCenterx1_Right {
    walk_left
}

script CastbelleTown_ProfessorPinesLab_EventScript_ProfessorPineGivesPokemon_Trigger5 {
    applymoveplayer(PlayerToCenterx2_Right)
    goto(CastbelleTown_ProfessorPinesLab_EventScript_ProfessorPineGivesPokemon)
    end
}

movement PlayerToCenterx2_Right {
    walk_left * 2
}

script CastbelleTown_ProfessorPinesLab_EventScript_ProfessorPineGivesPokemon_Trigger6 {
    applymoveplayer(PlayerToCenterx3_Right)
    goto(CastbelleTown_ProfessorPinesLab_EventScript_ProfessorPineGivesPokemon)
    end
}

movement PlayerToCenterx3_Right {
    walk_left * 3
}

script CastbelleTown_ProfessorPinesLab_EventScript_ProfessorPineTakePokemonBeforeLeaving {
    lock
    msgbox("Wait! Don't leave just yet!\p"
            "You have not chosen your partner\n"
            "Pokémon, yet!", MSGBOX_AUTOCLOSE)
    getplayerxy(VAR_0x8000, VAR_0x8001)
    switch (var(VAR_0x8000)) {
        case 5:
            applymoveplayer(PlayerBackToCenter_1)
        case 6:
            applymoveplayer(PlayerBackToCenter_2)
        case 7:
            applymoveplayer(PlayerBackToCenter_3)
        case 8:
            applymoveplayer(PlayerBackToCenter_4)
    }
    turnplayer(DIR_NORTH)
    release
    end
}

movement PlayerBackToCenter_1 {
    walk_up
    walk_right
    walk_up * 5
}

movement PlayerBackToCenter_2 {
    walk_up * 5
}

movement PlayerBackToCenter_3 {
    walk_left
    walk_up * 5
}

movement PlayerBackToCenter_4 {
    walk_up
    walk_left * 2
    walk_up * 5
}

script CastbelleTown_ProfessorPinesLab_EventScript_Aide1 {
    msgbox("I help out Prof. Pine in his research.\n"
            "He is a very passionate man!\p"
            "You'll learn a lot under him.\n"
            "I can guarentee that!", MSGBOX_NPC)
    faceoriginaldir
    end
}