const LOCALID_SAILOR = 5
const LOCALID_SPHEAL = 11

mapscripts SolaCity_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        setflag(FLAG_VISITED_SOLA_CITY)
    }
}

script SolaCity_EventScript_SolaCitySign {
    msgbox("Sola City\n"
            "“City of the blue sailors!”", MSGBOX_SIGN)
}

script SolaCity_EventScript_SeaMensHubSign {
    msgbox("Sea Men's Hub\n"
            "“Welcome the the sea!”", MSGBOX_SIGN)
}

script SolaCity_EventScript_HarborSign {
    msgbox("Sola Harbor\n"
            "“Cruising to the mainland!”", MSGBOX_SIGN)
}

script SolaCity_EventScript_InnsSign {
    msgbox("Sola Inns\n"
            "“Take a rest, forget the rest!”", MSGBOX_SIGN)
}

script SolaCity_EventScript_MarketSign {
    msgbox("Sola Market\n"
            "“Mainland goods for half the price!”", MSGBOX_SIGN)
}

script SolaCity_EventScript_Sailor1 {
    msgbox("Ahoy, matey!\n"
            "Welcome to Sola City!\p"
            "Enjoy the Sailors' City!", MSGBOX_NPC)
}

script SolaCity_EventScript_Sailor2 {
    msgbox("I need to get to work now.\n"
            "This places makes me forget how\l"
            "unforgiving the sea is!", MSGBOX_NPC)
}

script SolaCity_EventScript_Sailor3 {
    msgbox("Did you know?\p"
            "These safehouses are free of cost for\n"
            "everyone!\p"
            "Not just sailors, or sea men, but any\n"
            "traveller who wishes to take a rest.", MSGBOX_NPC)
}

script SolaCity_EventScript_Sailor4 {
    msgbox("I'm thinking of settling here.\n"
            "My family could move here, and I'd be\l"
            "able to see them everyday.\p"
            "Yeah!\n"
            "That's the dream!", MSGBOX_NPC)
}

script SolaCity_EventScript_Sailor5 {
    lock
    faceplayer
    goto_if_quest_complete(QUEST_2_BEAT_THE_SEA_MEN, SolaCity_EventScript_Sailor5_QuestComplete)
    goto_if_quest_reward(QUEST_2_BEAT_THE_SEA_MEN, SolaCity_EventScript_Sailor5_QuestReward)
    goto_if_quest_active(QUEST_2_BEAT_THE_SEA_MEN, SolaCity_EventScript_Sailor5_QuestCheck)
    msgbox("This building right here is called\n"
            "the Sea Men's Hub.\p"
            "Yes, you'll see a lot of sailors in\n"
            "there!\p"
            "You could also challenge them a battle.\n"
            "Those guys have caught a lot of\l"
            "Pokémon they caught during their time\l"
            "by the sea.\p"
            "A Trainer such as yourself would surely\n"
            "not miss such an opportunity, eh?", MSGBOX_YESNO)
    if (var(VAR_RESULT) == YES) {
        emote(LOCALID_SAILOR, EMOTE_EXCLAIM)
        msgbox("Ah ha!\p"
                "Beat them up, and I'll reward you\n"
                "handsomely!")
        startquest(QUEST_2_BEAT_THE_SEA_MEN)
    }
    else {
        msgbox("Ah, darn!\p"
                "In case you change your mind,\n"
                "you know where to find me.", MSGBOX_NPC)
    }
    release
    end
}

script SolaCity_EventScript_Sailor5_QuestCheck {
    msgbox("Did you beat those sailors yet?", MSGBOX_NPC)
}

script SolaCity_EventScript_Sailor5_QuestReward {
    emote(LOCALID_SAILOR, EMOTE_EXCLAIM)
    msgbox("What?! You beat them?\p"
            "Ahoy! You are good at this!\n"
            "Now for your reward.", MSGBOX_AUTOCLOSE)
    giveitem(ITEM_CUT_TOOL)
	goto_if_eq(VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull)
    msgbox("You got skills, kid!\p"
            "Keep training like this and no doubt\n"
            "one day you might just become the very\l"
            "best!", MSGBOX_AUTOCLOSE)
    completequest(QUEST_2_BEAT_THE_SEA_MEN)
    release
    end
}

script SolaCity_EventScript_Sailor5_QuestComplete {
    msgbox("You got skills, kid!\p"
            "Keep training like this and no doubt\n"
            "one day you might just become the very\l"
            "best!", MSGBOX_NPC)
}

script SolaCity_EventScript_TriathleteM {
    lock
    faceplayer
    if (getgamestat(GAME_STAT_STEPS) > 10000) {
        questmenu(QUEST_MENU_SET_REWARD, QUEST_3_KEEP_WALKING)
    }
    goto_if_quest_complete(QUEST_3_KEEP_WALKING, SolaCity_EventScript_TriathleteM_QuestComplete)
    goto_if_quest_reward(QUEST_3_KEEP_WALKING, SolaCity_EventScript_TriathleteM_QuestReward)
    goto_if_quest_active(QUEST_3_KEEP_WALKING, SolaCity_EventScript_TriathleteM_QuestCheck)
    msgbox("I love running!\n"
            "It's my passion.\p"
            "Do you like running?", MSGBOX_YESNO)
    if (var(VAR_RESULT) == YES) {
        msgbox("Ay, nice!\p"
                "Walk 10,000 steps and I'll give you a\n"
                "reward!", MSGBOX_AUTOCLOSE)
        startquest(QUEST_3_KEEP_WALKING)
    }
    else {
        msgbox("Running is good for your body!", MSGBOX_NPC)
    }
    release
    end
}

script SolaCity_EventScript_TriathleteM_QuestCheck {
    getgamestat(GAME_STAT_STEPS)
    copyvar(VAR_0x8000, VAR_RESULT)
    setvar(VAR_0x8001, 10000)
    subvar(VAR_0x8001, VAR_0x8000)
    copyvar(VAR_0x8000, VAR_0x8001)
    buffernumberstring(STR_VAR_1, VAR_0x8000)
    msgbox("Huh?\n"
            "You didn't walk 10,000 steps, yet!\p"
            "You still have {STR_VAR_1} steps left!", MSGBOX_NPC)
}

script SolaCity_EventScript_TriathleteM_QuestReward {
    msgbox("Yoo!\n"
            "You walked 10,000 steps!\p"
            "For that, I give you this.", MSGBOX_AUTOCLOSE)
    giveitem(ITEM_TM_BULK_UP)
	goto_if_eq(VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull)
    msgbox("Keep walking and stay healthy, kid!", MSGBOX_AUTOCLOSE)
    completequest(QUEST_3_KEEP_WALKING)
    release
    end
}

script SolaCity_EventScript_TriathleteM_QuestComplete {
    getgamestat(GAME_STAT_STEPS)
    copyvar(VAR_0x8000, VAR_RESULT)
    buffernumberstring(STR_VAR_1, VAR_0x8000)
    msgbox("Keep walking, {PLAYER}!\n"
            "You have walked over {STR_VAR_1} steps!", MSGBOX_NPC)
}

script SolaCity_EventScript_LittleBoy {
    lock
    msgbox("Here, Spheal! Look at me!", MSGBOX_AUTOCLOSE)
    release
    end
}

script SolaCity_EventScript_LittleGirl {
    lock
    msgbox("No! Spheal, look at me!", MSGBOX_AUTOCLOSE)
    release
    end
}

script SolaCity_EventScript_Woman {
    msgbox("I'm here to buy some berries and TM for\n"
            "my daughter.", MSGBOX_NPC)
    end
}

script SolaCity_EventScript_Spheal {
    emote(LOCALID_SPHEAL, EMOTE_QUESTION)
    moncryscript(SPECIES_SPHEAL, "Sphea?", CRY_MODE_FAINT)
    faceoriginaldir
    release
}

script SolaCity_EventScript_TMsSeller {
    lock
	faceplayer
	message(gText_HowMayIServeYou)
	waitmessage
	pokemart(TMsSeller_Items)
	msgbox(gText_PleaseComeAgain, MSGBOX_DEFAULT)
	release
	end 
}

mart TMsSeller_Items {
    ITEM_TM_HIDDEN_POWER
    ITEM_TM_SECRET_POWER
    ITEM_TM_PROTECT
}

script SolaCity_EventScript_BerrySeller1 {
    lock
	faceplayer
	message(gText_HowMayIServeYouBerryShop)
	waitmessage
    pokemart(BerrySeller1)
	msgbox(gText_PleaseComeAgain, MSGBOX_DEFAULT)
	release
	end 
}

mart BerrySeller1 {
    ITEM_CHILAN_BERRY
    ITEM_OCCA_BERRY
    ITEM_PASSHO_BERRY
    ITEM_WACAN_BERRY
    ITEM_RINDO_BERRY
    ITEM_YACHE_BERRY
    ITEM_CHOPLE_BERRY
    ITEM_KEBIA_BERRY
    ITEM_SHUCA_BERRY
}

script SolaCity_EventScript_BerrySeller2 {
    lock
	faceplayer
	message(gText_HowMayIServeYouBerryShop)
	waitmessage
    pokemart(BerrySeller2)
	msgbox(gText_PleaseComeAgain, MSGBOX_DEFAULT)
	release
	end 
}

mart BerrySeller2 {
    ITEM_COBA_BERRY
    ITEM_PAYAPA_BERRY
    ITEM_TANGA_BERRY
    ITEM_CHARTI_BERRY
    ITEM_KASIB_BERRY
    ITEM_HABAN_BERRY
    ITEM_COLBUR_BERRY
    ITEM_BABIRI_BERRY
    ITEM_ROSELI_BERRY
}
