const LOCALID_BRIAN = 1 # Brian is Wally

mapscripts CastbelleTown_BriansHouse_2F_MapScripts {
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_CASTBELLE_TOWN_STATE, 1: CastbelleTown_BriansHouse_2F_EventScript_BrianGettingReady
    ]
}

script CastbelleTown_BriansHouse_2F_EventScript_BrianGettingReady {
    lock
    delay(12)
    turnplayer(DIR_EAST)
    msgbox("Brian: …Aaaand, packed!\n"
            "I'm ready to get my starter Pokémon!\p"
            "Woody better let me pick the best one\n"
            "first!", MSGBOX_AUTOCLOSE)
    applymove(LOCALID_BRIAN, Common_Movement_FaceLeft)
    emote(LOCALID_BRIAN, EMOTE_EXCLAM)
    delay(6)
    applymove(LOCALID_BRIAN, MoveBrianToPlayer)
    msgbox("Oh! Hi, {PLAYER}!\p"
            "Excited to get your first Pokémon?\p"
            "Honestly, I'm kind of nervous…\p"
            "I've been going over my type matchups,\n"
            "like Prof. Pine told us to.\p"
            "You'd better be ready to battle when\n"
            "we get our Pokemon!\p"
            "Phew!\n"
            "Now, let's go!\p"
            "I can't wait to pick my starter!", MSGBOX_AUTOCLOSE)
    emote(LOCALID_BRIAN, EMOTE_EXCLAM)
    msgbox("I forgot,\n"
            "I gotta buy some berries first!\p"
            "Looks like I won't be the first one\n"
            "there, haha…", MSGBOX_AUTOCLOSE)
    applymovement(LOCALID_BRIAN, BrianWalkInPlaceLeft) // applymovement over applymove because these two should happen at the same time
    applymoveplayer(Common_Movement_StopInPlaceWalkLeft)
    applymove(LOCALID_BRIAN, BrianExit)
    delay(6)
    removeobject(LOCALID_BRIAN)
    playse(SE_EXIT)
    setvar(VAR_CASTBELLE_TOWN_STATE, 2)
    release
    end
}

movement MoveBrianToPlayer {
    walk_left * 6
}

movement BrianWalkInPlaceLeft {
    walk_in_place_fast_left
}

movement BrianExit {
    walk_left
    face_up
}

script CastbelleTown_BriansHouse_2F_BriansPC {
    msgbox(gText_PokemonTrainerSchoolEmail, MSGBOX_DEFAULT)
    releaseall
	end
}