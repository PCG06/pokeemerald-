const LOCALID_SHIP = 1
const LOCALID_SHIP_SHADOW = 2
const LOCALID_SAILOR = 3
const LOCALID_CAP_BRUCE = 4

// TODO: Add some traffic on Saturdays and Sundays

mapscripts SolaCity_Harbor_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        if (var(VAR_BRUCE_SHIP) == 2) {
            clearflag(FLAG_HIDE_SOLA_CITY_HARBOR_CAPTAIN_BRUCE)
        }
    }
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [
        VAR_BRUCE_SHIP, 2 {
            turnplayer(DIR_NORTH)
        }
    ]
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_BRUCE_SHIP, 2 {
            lock
            setvar(VAR_BRUCE_SHIP, 0)
            speakername("Captain Bruce")
            msgbox("We reached Sola City!\p"
                    "Let me know when we're sailing to\n"
                    "Mistfall.", MSGBOX_NPC)
            release
            end
        }
    ]
}

script SolaCity_Harbor_EventScript_Sailor {
    if (var(VAR_BRUCE_SHIP) != 0) {
        msgbox("I am sorry, but the ship won't set sail\n"
                "til tomorrow.\p"
                "The captain's has gone on a break.", MSGBOX_NPC)
    }
    else {
        msgbox("Captain's back!\n"
                "We'll set sail soon!", MSGBOX_NPC)
    }
    release
    end
}

script SolaCity_Harbor_EventScript_CaptainBruce {
    lock
    faceplayer
    speakername("Captain Bruce")
    msgbox("So, you wanna set sail now, {PLAYER}?", MSGBOX_YESNO)
    if (var(VAR_RESULT) == YES)
    {
        applymove(LOCALID_CAP_BRUCE, Common_Movement_WalkInPlaceDown)
        speakername("Captain Bruce")
        msgbox("Arr we go!", MSGBOX_AUTOCLOSE)
        applymove(LOCALID_CAP_BRUCE, Common_Movement_WalkInPlaceUp)
        removeobject(LOCALID_CAP_BRUCE)
        clearflag(FLAG_HIDE_MISTFALL_CITY_HARBOR_CAPTAIN_BRUCE)
        applymoveplayer(Player_WalkUp)
        delay(30)
        setvar(VAR_BRUCE_SHIP, 1)
        warpsilent(MAP_MISTFALL_CITY_HARBOR, 8, 11)
        end
    }
    else {
        applymove(LOCALID_CAP_BRUCE, Common_Movement_WalkInPlaceDown)
        speakername("Captain Bruce")
        msgbox("Arr! Okay then.", MSGBOX_AUTOCLOSE)
        release
        end
    }
}

movement Player_WalkUp {
    walk_up
    walk_in_place_up
    delay_8
	set_invisible
}

script SolaCity_Harbor_EventScript_Fisherman {
    msgbox("This harbor is busiest on the weekends.\n"
            "Loads of cargo gets dropped off and\l"
            "picked up here.", MSGBOX_NPC)
}
