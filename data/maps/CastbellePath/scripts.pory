const LOCALID_BRIAN = 1

mapscripts CastbellePath_MapScripts {}

script CastbellePath_EventScript_SignPost_North {
    msgbox("Castbelle Path\n"
            "{UP_ARROW} Sola City", MSGBOX_SIGN)
}

script CastbellePath_EventScript_SignPost_South {
    msgbox("Castbelle Path\n"
            "{DOWN_ARROW} Castbelle Town", MSGBOX_SIGN)
}

script CastbellePath_EventScript_TrainerTips {
    msgbox("Trainer Tips\p"
	        "In addition to Hit Points (HP), Pokémon\n"
	        "have Power Points (PP) that are used to\l"
	        "make moves during battle.\p"
	        "If a Pokémon runs out of PP, it must be\n"
	        "taken to a Pokémon Center.\p"
            "The closest Pokémon Center is in Sola\n"
            "City!", MSGBOX_SIGN)
}

script CastbellePath_EventScript_Brian {
    lock
    faceplayer
    speakername("Brian")
    msgbox("Hey, {PLAYER}!\p"
            "I caught some Pokémon here.\n"
            "Wanna battle me?", MSGBOX_YESNO)
    if (var(VAR_RESULT) == YES) {  
        msgbox("Alright! Here I come!", MSGBOX_DEFAULT)
        switch (var(VAR_STARTER_MON))
        {
            default:
            case STARTER_GRASS:
                trainerbattle_no_intro(TRAINER_BRIAN_2_GRASS, "You gotta be kidding me!\pNo way!")

            case STARTER_FIRE:
                trainerbattle_no_intro(TRAINER_BRIAN_2_FIRE, "You gotta be kidding me!\pNo way!")

            case STARTER_WATER:
                trainerbattle_no_intro(TRAINER_BRIAN_2_WATER, "You gotta be kidding me!\pNo way!")
        }
        playbgm(MUS_DP_RIVAL, FALSE)
        speakername("Brian")
        msgbox("Man! You're strong!\n"
                "That was a fun battle!", MSGBOX_AUTOCLOSE)
        msgbox("… … … … … … …", MSGBOX_AUTOCLOSE)
        speakername("Brian")
        msgbox("Huh? My Pokédex?\n"
                "Couldn't you have started with that?\p"
                "Would have saved me some shame!", MSGBOX_AUTOCLOSE)
        playfanfare(MUS_HG_OBTAIN_TMHM)
        message("{PLAYER} handed over the Pokédex!")
        waitfanfare
        waitmessage
        speakername("Brian")
        msgbox("Thank you, {PLAYER}!\n"
                "I am quite hasty, haha.\p"
                "Now, I'm headed towards Sola City to\n"
                "catch the next ferry!\p"
                "I expect you to be there as well!\n"
                "Don't be late!", MSGBOX_NPC)
        setflag(FLAG_SAFE_FOLLOWER_MOVEMENT)
        getplayerxy(VAR_TEMP_1, VAR_TEMP_2)
        if (var(VAR_TEMP_1) == 4 && var(VAR_TEMP_2) == 13) {
            applymove(LOCALID_BRIAN, BrianExitsCastbellePath_1)
        }
        else {
            applymove(LOCALID_BRIAN, BrianExitsCastbellePath_2)
        }
        fadedefaultbgm
        removeobject(LOCALID_BRIAN)
        setvar(VAR_MAIN_STORY, 5)
        setvar(VAR_CASTBELLE_TOWN_STATE, 6)
        if(islevelcapactive) {
            setvar(B_LEVEL_CAP_VARIABLE, 17)
        }
    }
    else {
        speakername("Brian")
        msgbox("Weak.\p"
                "I'll be here for another while.\n"
                "I think I saw a rare Pokémon!", MSGBOX_AUTOCLOSE)
    }
    release
    end
}

movement BrianExitsCastbellePath_1 {
    walk_up * 2
    walk_right * 2
    walk_up * 5
}

movement BrianExitsCastbellePath_2 {
    walk_right * 2
    walk_up * 7
}

script CastbellePath_EventScript_Boy {
    msgbox("I'm trying to catch a Pidgey here.\n"
            "Hmm…\l"
            "I can't seem to find one anywhere.", MSGBOX_NPC)
}

script CastbellePath_EventScript_Woman {
    msgbox("Oh, no! I'm not trapped here.\n"
            "Hmm…\l"
            "The ledges let us jump down from here.", MSGBOX_NPC)
}

script CastbellePath_EventScript_Girl {
    msgbox("Did you know?\p"
            "You can go back to Castbelle Town from\n"
            "here without walking on a single\l"
            "grass patch!\p"
            "How?\n"
            "Use the ledges on the left side!", MSGBOX_NPC)
}

script CastbellePath_EventScript_OldLady {
    msgbox("Walking on this grass path heal my tired,\n"
            "old feet.", MSGBOX_NPC)
}

script CastbellePath_EventScript_YoungsterJoey {
    trainerbattle_single(TRAINER_JOEY, "I love wearing shorts!\nThey are comfy and easy to wear!", "You got no chill!")
    msgbox("You seem ill-tempered!\n"
            "Wearing shorts would fix that!", MSGBOX_NPC)
}

script CastbellePath_EventScript_TriggerMeetBrian {
    lock
    msgbox("I should give Brian his Pokédex!", MSGBOX_AUTOCLOSE)
	setflag(FLAG_SAFE_FOLLOWER_MOVEMENT)
    applymoveplayer(Common_Movement_WalkDown)
    release
    end
}