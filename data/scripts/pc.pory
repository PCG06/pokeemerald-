raw `
EventScript_PC::
	lockall
	setvar VAR_0x8004, PC_LOCATION_OTHER
	special DoPCTurnOnEffect
	playse SE_PC_ON
	msgbox Text_BootUpPC, MSGBOX_DEFAULT
	goto EventScript_PCMainMenu
	end

EventScript_PCMainMenu::
	message gText_WhichPCShouldBeAccessed
	waitmessage
	special ScriptMenu_CreatePCMultichoice
	waitstate
	goto EventScript_AccessPC
	end

EventScript_AccessPC::
	switch VAR_RESULT
	case 0, EventScript_AccessPokemonStorage
	case 1, EventScript_AccessPlayersPC
	case 2, EventScript_AccessHallOfFame
	case 3, EventScript_TurnOffPC
	case MULTI_B_PRESSED, EventScript_TurnOffPC
	end

EventScript_AccessPlayersPC::
	playse SE_PC_LOGIN
	msgbox gText_AccessedPlayersPC, MSGBOX_DEFAULT
	special PlayerPC
	waitstate
	goto EventScript_PCMainMenu
	end

EventScript_AccessPokemonStorage::
	playse SE_PC_LOGIN
	call_if_unset FLAG_SYS_PC_LANETTE, EventScript_AccessSomeonesPC
	call_if_set FLAG_SYS_PC_LANETTE, EventScript_AccessLanettesPC
	msgbox gText_StorageSystemOpened, MSGBOX_DEFAULT
	special ShowPokemonStorageSystemPC
	waitstate
	goto EventScript_PCMainMenu
	end

EventScript_AccessSomeonesPC::
	msgbox gText_AccessedSomeonesPC, MSGBOX_DEFAULT
	return

EventScript_AccessLanettesPC::
	msgbox gText_AccessedLanettesPC, MSGBOX_DEFAULT
	return

EventScript_TurnOffPC::
	setvar VAR_0x8004, PC_LOCATION_OTHER
	playse SE_PC_OFF
	special DoPCTurnOffEffect
	releaseall
	end

EventScript_AccessHallOfFame::
	goto_if_unset FLAG_SYS_GAME_CLEAR, EventScript_TurnOffPC
	playse SE_PC_LOGIN
	special AccessHallOfFamePC
	waitstate
	goto EventScript_AccessPC
	end	
`
script EventScript_AccessPokemonBoxLink {
	lockall
	playse(SE_PC_ON)
	message("{PKMN} Box Link opened.\n")
	waitse
	call(EventScript_AccessPokemonBoxLinkDynMultiChoice)
	goto(BoxLink_LogOffPC)
	end
}

script EventScript_AccessPokemonBoxLinkDynMultiChoice {
	message("What will you like to do?")
	if (flag(FLAG_SYS_PC_LANETTE) == true) {
		bufferstring(STR_VAR_1, "Lanette")
	}
	else {
		bufferstring(STR_VAR_1, "Someone")
	}
	dynmultichoice(0, 0, FALSE, 3, 0, DYN_MULTICHOICE_CB_NONE, "{STR_VAR_1}'s Storage", "Heal PokÃ©mon", "See ya!")
	closemessage
	switch (var(VAR_RESULT))
	{
		case 0:
			playse(SE_PC_LOGIN)
			call_if_unset(FLAG_SYS_PC_LANETTE, EventScript_AccessSomeonesPC)
			call_if_set(FLAG_SYS_PC_LANETTE, EventScript_AccessLanettesPC)
			msgbox(gText_StorageSystemOpened, MSGBOX_DEFAULT)
			special(ShowPokemonStorageSystemPC)
			waitstate
			playse(SE_PC_OFF)
			goto(BoxLink_ContinueUsingPC)
		
		case 1:
			if (var(VAR_POKEMON_BOX_LINK_HEAL) > 0) {
				special(HealPlayerParty)
				playse(MUS_HEAL)
				message("Your party has been healed!\n")
				waitmessage
				waitse
				closemessage
				subvar(VAR_POKEMON_BOX_LINK_HEAL, 1)
				if (var(VAR_POKEMON_BOX_LINK_HEAL) > 0) {
					buffernumberstring(STR_VAR_2, VAR_POKEMON_BOX_LINK_HEAL)
					msgbox("Heals left: {STR_VAR_2}.", MSGBOX_AUTOCLOSE)
				}
				else {
					msgbox("No heals left!", MSGBOX_AUTOCLOSE)
				}
				goto(BoxLink_ContinueUsingPC)
			}
			else {
				playse(SE_FAILURE)
				waitse
				msgbox("No heals left!", MSGBOX_AUTOCLOSE)
				goto(BoxLink_ContinueUsingPC)
			}

		case 2:
			goto(BoxLink_LogOffPC)
			end
	}
	goto(BoxLink_LogOffPC)
	end
}

script BoxLink_ContinueUsingPC {
	lockall
	message("What will you like to do?")
	call(EventScript_AccessPokemonBoxLinkDynMultiChoice)
	goto(BoxLink_LogOffPC)
	end
}

script BoxLink_LogOffPC {
	playse(SE_PC_OFF)
	releaseall
	end
}
